uniform sampler2D	u_tex, u_noise;
uniform float		u_strength, u_mono, u_bias;
uniform	vec2		u_ibs, u_random;

void	main()
{
	vec2	ref_uv = vec2(gl_FragCoord.x, gl_FragCoord.y) * u_ibs;
	vec4	ref = texture2D(u_tex, ref_uv);

	vec2	noise_uv = vec2(gl_FragCoord.x, gl_FragCoord.y) / 128.0;
	vec4	noise_a = texture2D(u_noise, noise_uv + vec2(u_random.x * 3.456, u_random.x * 7.145)),
			noise_b = texture2D(u_noise, noise_uv + vec2(u_random.y * 2.789, u_random.y * 9.781));

	vec4	noise = noise_a.r > 0.5 ? noise_b.barg : noise_a.rgba;
	noise = mix(noise, noise.rrrr, u_mono);

	//--
	float	strength = u_strength * 6.0;

	float	luma = dot(ref.rgb, vec3(1.0 / 3.0, 1.0 / 3.0, 1.0 / 3.0));

	if	(u_bias < 0.5)
		strength *= pow(clamp(1.0 + luma * (u_bias - 0.5) * 2.0, 0.0, 1.0), 5.0);
	if	(u_bias > 0.5)
		strength *= pow(clamp(1.0 - (1.0 - luma) * (u_bias - 0.5) * 2.0, 0.0, 1.0), 5.0);

	gl_FragColor = vec4(ref.rgb * (1.0 + (noise.rgb - 0.5) * strength * 2.0), ref.a);
}
